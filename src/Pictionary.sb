' Pictionary
' Version 0.91
' Copyright Â© 2017-2019 Nonki Takahashi.  The MIT License.
' Last update 2019-04-19
' Extension LitDev

title = "Pictionary 0.91"
GraphicsWindow.Title = title
GraphicsWindow.BackgroundColor = "AntiqueWhite"
kw = "And,Array,Bell,Box,Brush,"
kw = kw + "Ceiling,Chime,Chinese,Clock,Color,Control,Cosine,"
kw = kw + "Date,Degree,Desktop,Dictionary,Draw,"
kw = kw + "Ellipse,English,Error,File,Fill,Flickr,Floor,French,"
kw = kw + "German,Go,Graphics,Height,Hour,Image,Italian,Japanese,"
kw = kw + "Korean,Length,Line,Math,Minute,Mouse,Music,Network,Or,"
kw = kw + "Paper,Pen,Program,"
kw = kw + "Radian,Random,Rectangle,Resize,RGB,Rotate,"
kw = kw + "Second,Shape,Shell,Sine,Sound,Spanish,Square,Stack,Stop,"
kw = kw + "Tangent,Text,Timer,Triangle,Turtle,"
kw = kw + "Wall,Width,Window,Write"
keywords = LDText.Split(kw, ",")
n = Array.GetItemCount(keywords)
GraphicsWindow.Title = title + " - " + n + " keywords"
LDSpeech.Voice = "Microsoft Zira Desktop"
GraphicsWindow.BrushColor = "Black"
GraphicsWindow.FontName = "Trebuchet MS"
xLeft = 10
y[0] = -20
dx = 80
For i = 1 To n
  y[i] = y[i - 1] + 30
  If 400 < y[i] Then
    xLeft = xLeft + dx
    y[i] = 10
  EndIf
  x[i] = xLeft
  GraphicsWindow.DrawText(x[i], y[i], keywords[i])
EndFor
GraphicsWindow.MouseDown = OnMouseDown
LDSpeech.Speed = -1
While "True"
  If mouseDown Then
    For i = 1 To n
      If x[i] <= mx And mx <= x[i] + dx And y[i] <= my And my <= y[i] + 12 Then
        LDProcess.Start(Program.Directory + "\" + keywords[i] + ".exe", "")
        LDSpeech.Speak(keywords[i])
        If keywords[i] = "Bell" Then
          Sound.PlayBellRing()
        ElseIf keywords[i] = "Chime" Then
          Sound.PlayChime()
        ElseIf keywords[i] = "Click" Then
          Sound.PlayClick()
        ElseIf keywords[i] = "Music" Then
          Sound.PlayMusic("O5C8D8E8F8G8A8B8O6C4O5G4O6C4")
        EndIf
      EndIf
    EndFor
    mouseDown = "False"
  EndIf
  Program.Delay(200)
EndWhile

Sub OnMouseDown
  mouseDown = "True"
  mx = GraphicsWindow.MouseX
  my = GraphicsWindow.MouseY
EndSub
